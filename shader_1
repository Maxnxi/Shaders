vec3 WHITE = vec3(1);
vec3 RED = vec3(178,34,52)/255.;
vec3 BLUE = vec3(60, 59, 110)/255.;
float FR = 3.1415*2.; // FULLRotation

float Star(vec2 uv, float size) {
    uv.x = abs(uv.x);
    
    
    float a = FR/5.;
    float d1 = dot(uv, vec2(sin(a), cos(a)));
    
    a = FR/5.*3.;
    float d2 = dot(uv, vec2(sin(a), cos(a)));
    
    a = FR/5.*2.;
    float d4 = dot(uv, vec2(sin(a), cos(a)));

    float d = min(max(d1,d2),max(uv.y,d4));
    float w = min(fwidth(d), .05);
    
    return smoothstep(w,-w,d-size);
}

vec2 Remap(vec2 p, float b, float l, float t, float r) {
    return vec2( (p.x-l) / (r-l), (p.y-b) / (t-b));
}

void mainImage( out vec4 fragColor, in vec2 fragCoord )
{
    vec2 uv = fragCoord/iResolution.xy;
    
    float w = fwidth(uv.y);
    float stripes = smoothstep(-w, w, sin(uv.y*3.1415*13.));
    vec3 col = mix(WHITE, RED, stripes);
    
    vec2 st = Remap(uv, .46, 0.,1., .4);
        
    if(st.x>0. && st.x<1. && st.y>0. && st.y<1.) {
        vec2 gv = fract( st*vec2(6,5) )-.5;
        col = mix(BLUE, WHITE, Star(gv, .1));
        
        st = Remap(st, .1, .0833, .9, .9166);
        if(st.x>0. && st.x<1. && st.y>0. && st.y<1.) {
            vec2 gv = fract( st*vec2(5,4) )-.5;
            col = mix(col, WHITE, Star(gv, .1));
        }
    }

    fragColor = vec4(col,1.0);
}